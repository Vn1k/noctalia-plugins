import QtQuick
import Quickshell
import Quickshell.Io
import qs.Commons
import qs.Services.UI

Item {
    id: root
    property var pluginApi: null

    readonly property string configBase: Quickshell.env("XDG_CONFIG_HOME") || (Quickshell.env("HOME") + "/.config")
    readonly property string configDir: configBase + "/niri"
    readonly property string mainConfig: configDir + "/config.kdl"
    readonly property string layoutConfig: configDir + "/layout.kdl"

    Component.onCompleted: {
        Logger.i("NiriDebug", "Main.qml Loaded! Checking IPC status...")
        setupProcess.running = true
    }

    IpcHandler {
        id: ipc
        target: "niri-layout-mode" 

        Component.onCompleted: {
            Logger.i("NiriDebug", "IPC Handler Registered with target name: " + target)
        }

        function getMode() {
            Logger.d("NiriDebug", "getMode() called via IPC")
            
            if (!root.pluginApi) {
                Logger.w("NiriDebug", "pluginApi is NULL. Returning default 'center'.")
                return "center"
            }
            
            var mode = root.pluginApi.pluginSettings.mode || "center"
            Logger.i("NiriDebug", "Returning mode: " + mode)
            return mode
        }

        function toggle() {
             Logger.d("NiriDebug", "toggle() called via IPC")
             var current = root.pluginApi ? (root.pluginApi.pluginSettings.mode || "center") : "center"
             var nextMode = (current === "center" ? "split" : "center")

             root.setMode(nextMode)
             return nextMode
        }
    }

    function setMode(modeName) {
        if (!pluginApi) return 

        var targetMode = modeName.toLowerCase()
        var centerVal = "always"
        var label = "Center Mode"

        if (targetMode === "split") {
            centerVal = "never"
            label = "Split Mode"
        } 

        var fileContent = `// Auto-generated by Niri Layout Switcher
layout {
    center-focused-column "${centerVal}"
}`
        
        var cmd = `printf '${fileContent}' > "${root.layoutConfig}" && niri msg action reload-config-file`

        layoutProcess.command = ["bash", "-c", cmd]
        layoutProcess.running = true

        pluginApi.pluginSettings.mode = targetMode
        pluginApi.saveSettings()
        
        ToastService.showNotice("Layout: " + label)
    }

    Process {
        id: setupProcess
        command: ["bash", "-c", root.installScript]

        stdout: SplitParser {
            onRead: (data) => {
                Logger.d("NiriLayout", "Script Output: " + data)
                if (data.trim() === "CHANGED") {
                    reloadProcess.running = true
                    ToastService.showNotice(qsTr("Plugin installed & Layout injected."))
                }
            }
        }
    }

    
    Process {
        id: reloadProcess
        command: ["niri", "msg", "action", "reload-config-file"]
    }

    Process { id: layoutProcess }
    readonly property string installScript: `
        DIR="${root.configDir}"
        MAIN="${root.mainConfig}"
        LAYOUT="${root.layoutConfig}"
        
        mkdir -p "$DIR"

        if [ ! -f "$LAYOUT" ]; then
            echo '// Auto-generated by Plugin' > "$LAYOUT"
            echo 'layout { center-focused-column "always"; }' >> "$LAYOUT"
        fi

        if ! grep -q 'include "layout.kdl"' "$MAIN"; then
            # Backup
            cp "$MAIN" "$MAIN.bak"
            
            # Append include
            echo '' >> "$MAIN"
            echo '// Auto-added by Niri Layout Switcher Plugin' >> "$MAIN"
            echo 'include "layout.kdl"' >> "$MAIN"
            
            echo "CHANGED"
        fi
    `
}